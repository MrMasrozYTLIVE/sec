plugins {
    id 'fabric-loom' version '1.14-SNAPSHOT'
    id "com.hypherionmc.modutils.modpublisher" version "2.1.8+snapshot.4"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}


repositories {
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url "https://maven.midnightdust.eu/releases" }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "eu.midnightdust:midnightlib:${project.midnightlib_version}"
    include "eu.midnightdust:midnightlib:${project.midnightlib_version}"

    // Compat
    modApi "com.terraformersmc:modmenu:${project.modmenu_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    inputs.property "midnightlib_version", project.midnightlib_version
    inputs.property "bbe_version", project.bbe_version
    inputs.property "modmenu_version", project.modmenu_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version,
                "midnightlib_version": project.midnightlib_version,
                "bbe_version": project.bbe_version,
                "modmenu_version": project.modmenu_version
    }
}

def targetJavaVersion = 21
tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archives_base_name}" }
    }
}

publisher {
    apiKeys {
        modrinth System.getenv("MODRINTH_TOKEN")
        github System.getenv("GITHUB_TOKEN")
    }

//    setDebug(true)
    setModrinthID(project.modrinth_id)
    setVersionType(project.version_type)

    github {
        repo = project.github_repo
        tag = "${project.minecraft_version}-${project.version}"
    }

    // Changelog. This can be a file, string, OR, gist/github url
    // For example: markdown.md, or "This is my changelog"
    // Or: https://raw.githubusercontent.com/hypherionmc/changelogs/changelog.md
    // Or https://gist.githubusercontent.com/hypherionmc/92f825d3c9337964cc77c9c8c9bf65e6/raw/ceeaaee5b98c688a23398864fe480b84796a1651/test_gist.md
    setChangelog(file("changelog.md").text)

    setProjectVersion("${project.minecraft_version}-${project.version}")
    setGameVersions(project.minecraft_version)
    setLoaders("fabric")
    setArtifact(jar)
    addAdditionalFile(sourcesJar)

    modrinthDepends {
        embedded("midnightlib")
    }
}
